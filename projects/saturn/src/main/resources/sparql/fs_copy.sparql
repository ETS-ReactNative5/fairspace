# Moves a file or a directory from one location to another.
# Completely overwrites the target.
#
# 0 - from
# 1 - to
# 2 - label

PREFIX fs: <http://fairspace.io/ontology#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>


# Create the target resource
INSERT { ?newIRI fs:filePath %2$s; rdfs:label %3$s . }
WHERE {
    ?s fs:filePath %1$s .
    FILTER(NOT EXISTS {?s fs:dateDeleted ?dateDeleted})
    BIND(IRI(CONCAT(STR(ws:), STRUUID())) as ?newIRI)
};

# Copy statements
INSERT { ?newIRI ?p ?o }
WHERE {
    ?newIRI fs:filePath %2$s .

    ?s ?p ?o ;
    fs:filePath %1$s .
    FILTER(NOT EXISTS {?s fs:dateDeleted ?dateDeleted})
    FILTER( ?p != fs:filePath) # filePath has changed
    FILTER( ?p != rdfs:label)
};

# Create children
INSERT { ?newIRI fs:filePath ?newPath }
WHERE {
    ?s fs:filePath ?oldPath .

    FILTER(STRSTARTS(?oldPath, CONCAT(%1$s, '/')))
    FILTER(NOT EXISTS {?s fs:dateDeleted ?dateDeleted})
    BIND(IRI(CONCAT(STR(ws:), STRUUID())) as ?newIRI)
    BIND(CONCAT(%2$s, '/', SUBSTR(?oldPath, STRLEN(%1$s) + 2)) as ?newPath)
};

# Copy children's statements
INSERT { ?newIRI ?p ?o }
WHERE {
    ?newIRI fs:filePath ?newPath .
    FILTER(STRSTARTS(?newPath, CONCAT(%2$s, '/')))
    BIND(CONCAT(%1$s, '/', SUBSTR(?newPath, STRLEN(%2$s) + 2)) as ?oldPath)
    ?s fs:filePath ?oldPath .
    ?s ?p ?o .
    FILTER(NOT EXISTS {?s fs:dateDeleted ?dateDeleted})
    FILTER((?p != fs:filePath)) # filePath has changed
};