# Moves a file or a directory from one location to another.
# Completely overwrites the target.
#
# 0 - from
# 1 - to
# 2 - base uri

PREFIX fs: <http://fairspace.io/ontology#>

# Delete an existing "to" resource
DELETE { ?s ?p ?o }
WHERE { ?s fs:filePath ?1 };

# Create the target resource
INSERT { ?newIRI fs:filePath ?1 }
WHERE {
    ?s fs:filePath ?0 .
    BIND(IRI(CONCAT(?2, STRUUID())) as ?newIRI)
};

# Copy statements
INSERT { ?newIRI ?p ?o }
WHERE {
    ?newIRI fs:filePath ?1 .

    ?s ?p ?o ;
    fs:filePath ?0 .
    FILTER( ?p != fs:filePath) # filePath has changed
};

# Delete target's children
DELETE { ?s ?p ?o }
WHERE {
  ?s fs:filePath ?path .
  FILTER(STRSTARTS(?path, CONCAT(?1, '/')))
  ?s ?p ?o .
};

# Create children
INSERT { ?newIRI fs:filePath ?newPath }
WHERE {
    ?s fs:filePath ?oldPath .

    FILTER(STRSTARTS(?oldPath, CONCAT(?0, '/')))
    BIND(IRI(CONCAT(?2, STRUUID())) as ?newIRI)
    BIND(CONCAT(?1, '/', SUBSTR(?oldPath, STRLEN(?0) + 2)) as ?newPath)
};

# Copy children's statements
INSERT { ?newIRI ?p ?o }
WHERE {
    ?newIRI fs:filePath ?newPath .
    FILTER(STRSTARTS(?newPath, CONCAT(?1, '/')))
    BIND(CONCAT(?0, '/', SUBSTR(?newPath, STRLEN(?1) + 2)) as ?oldPath)
    ?s fs:filePath ?oldPath .
    ?s ?p ?o .
    FILTER((?p != fs:filePath)) # filePath has changed
};