# Updates a collection
#
# 0 - IRI
# 1 - name
# 2 - description
# 3 - directory name
# 4 - updated by

PREFIX fs: <http://fairspace.io/ontology#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>


# Move the children
DELETE { ?s fs:filePath ?path }
INSERT { ?s fs:filePath ?newPath}
WHERE {
  ?0 fs:filePath ?oldPath.
  ?s fs:filePath ?path .
  FILTER(STRSTARTS(?path, CONCAT(?oldPath, '/')))
  FILTER(NOT EXISTS {?s fs:dateDeleted ?dateDeleted})
  BIND(CONCAT(?3, '/', SUBSTR(?path, STRLEN(?oldPath) + 2)) as ?newPath)
};

# Update the collection
DELETE { ?0 rdfs:label ?name; rdfs:comment ?comment; fs:filePath ?oldPath; fs:dateModified ?oldDateModified; fs:modifiedBy ?oldModifiedBy; }
INSERT { ?0 rdfs:label ?1; rdfs:comment ?2; fs:filePath ?3; fs:dateModified ?date; fs:modifiedBy ?4 ; }
WHERE { { ?0 a fs:Collection; rdfs:label ?name; rdfs:comment ?comment; fs:filePath ?oldPath; }
OPTIONAL { ?0 fs:dateModified ?oldDateModified; fs:modifiedBy ?oldModifiedBy;}
BIND(NOW() as ?date) };