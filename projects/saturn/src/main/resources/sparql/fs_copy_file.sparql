# Copies a file from one location to another.
# Completely overwrites the target.
#
# 0 - from
# 1 - to
# 2 - label

PREFIX fs: <http://fairspace.io/ontology#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>


# Create the target resource
INSERT {
    ?newIRI fs:filePath %2$s;
            rdfs:label %3$s .
    ?s fs:tempNewIRI ?newIRI . }
WHERE {
    ?s fs:filePath %1$s .
    FILTER(NOT EXISTS {?s fs:dateDeleted ?dateDeleted})
    BIND(IRI(CONCAT(STR(ws:), STRUUID())) as ?newIRI)
};

# Copy statements
INSERT { ?newIRI ?p ?o }
WHERE {
    ?s fs:tempNewIRI ?newIRI ; ?p ?o ;

    FILTER( ?p != fs:filePath) # filePath has changed
    FILTER( ?p != rdfs:label)
};

DELETE { ?s fs:tempNewIRI ?o } WHERE { ?s fs:tempNewIRI ?o };